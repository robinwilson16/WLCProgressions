CREATE PROCEDURE [dbo].[SPR_PRG_GetStudentList]
	@System NVARCHAR(50),
    @SystemILP NVARCHAR(50),
    @AcademicYear NVARCHAR(5),
    @CourseID int,
	@GroupID int
AS
BEGIN
	SET XACT_ABORT, NOCOUNT ON;
	SET DATEFORMAT ymd;

	--DECLARE @System NVARCHAR(50) = 'ProSolution'
	--DECLARE @SystemILP NVARCHAR(50) = 'PROMONITOR.ProMonitor'
	--DECLARE @AcademicYear NVARCHAR(5) = '21/22'
	--DECLARE @CourseID int = 38770
	--DECLARE @GroupID int = 46323


	DECLARE @NextYear VARCHAR(5) = CAST ( CAST ( LEFT ( @AcademicYear, 2 ) AS INT ) + 1 AS VARCHAR(2) ) + '/' + CAST ( CAST ( RIGHT ( @AcademicYear, 2 ) AS INT ) + 1 AS VARCHAR(2) )
    DECLARE @DestinationYear NVARCHAR(5)
    SET @DestinationYear = 
        ( 
            SELECT
                AcademicYear = 
                    CASE 
                        WHEN MONTH ( GETDATE() ) >= 10 THEN CAST ( YEAR ( GETDATE() ) - 2000 AS NVARCHAR(2) ) + '/' + CAST ( YEAR ( GETDATE() ) - 1999 AS NVARCHAR(2) )
                        ELSE CAST ( YEAR ( GETDATE() ) - 2001 AS NVARCHAR(2) ) + '/' + CAST ( YEAR ( GETDATE() ) - 2000 AS NVARCHAR(2) )
                    END 
        )

    DECLARE @SQLString NVARCHAR(MAX);
    DECLARE @SQLParams NVARCHAR(MAX);

	--Attendance by Learner and Course
	SET @SQLString = 
    N'
	DROP TABLE IF EXISTS #AttendC
	SELECT
        SD.StudentDetailID,
        ENR.OfferingID,
        ENR.OfferingGroupID,
        Planned = COUNT ( COALESCE ( RSES.RegisterScheduleID, RSES.RegisterSessionID ) ),
        Counted = SUM ( CASE WHEN COALESCE ( MTS.MarkTypeStatusID, 0 ) <> 2 THEN 1 ELSE 0 END ),
        Marked = SUM ( CASE WHEN RM.MarkTypeID IS NOT NULL THEN 1 ELSE 0 END ),
        Unmarked = SUM ( CASE WHEN RM.MarkTypeID IS NULL THEN 1 ELSE 0 END ),
        Present = SUM ( CASE WHEN MTS.MarkTypeStatusID = 1 THEN 1 ELSE 0 END ),
        Absent = SUM ( CASE WHEN MTS.MarkTypeStatusID = 0 THEN 1 ELSE 0 END ),
        Neutral = SUM ( CASE WHEN MTS.MarkTypeStatusID = 2 THEN 1 ELSE 0 END ),
        AuthAbsence = SUM ( CASE WHEN MT.IsAuthorisedAbsence = 1 THEN 1 ELSE 0 END ),
        Late = SUM ( CASE WHEN MT.IsLate = 1 THEN 1 ELSE 0 END ),
        AttendPer = 
            ROUND (
                CASE
                    WHEN SUM ( CASE WHEN COALESCE ( MTS.MarkTypeStatusID, 0 ) <> 2 THEN 1 ELSE 0 END ) = 0 THEN 0
                    ELSE 
                        CAST ( SUM ( CASE WHEN MTS.MarkTypeStatusID = 1 THEN 1 ELSE 0 END ) AS FLOAT )
                        /
                        CAST ( SUM ( CASE WHEN COALESCE ( MTS.MarkTypeStatusID, 0 ) <> 2 THEN 1 ELSE 0 END ) AS FLOAT )
                END
            , 4 )
		INTO #AttendC
    FROM ' + @System + '.dbo.StudentDetail SD
    INNER JOIN ' + @System + '.dbo.Enrolment ENR
        ON ENR.StudentDetailID = SD.StudentDetailID
    INNER JOIN ' + @System + '.dbo.Offering CRS
        ON CRS.OfferingID = ENR.OfferingID
    INNER JOIN ' + @System + '.dbo.RegisterStudent REGS
        ON REGS.EnrolmentID = ENR.EnrolmentID
    INNER JOIN ' + @System + '.dbo.Register REG
        ON REG.RegisterID = REGS.RegisterID
	'

	SET @SQLString += 
        N'
    INNER JOIN (
		SELECT
			RegisterID = REG.RegisterID,
			RegisterScheduleID = REGSC.RegisterScheduleID,
			RegisterSessionID = RSES.RegisterSessionID,
			Date = REGSC.Date,
			StartTime = FORMAT ( REGSC.StartTime, ''HH:mm:ss'' ),
			EndTime = FORMAT ( REGSC.EndTime, ''HH:mm:ss'' ),
			Duration = REGSC.Duration
		FROM ' + @System + '.dbo.Register REG
		INNER JOIN ' + @System + '.dbo.RegisterSchedule REGSC
			ON REGSC.RegisterID = REG.RegisterID
		LEFT JOIN ' + @System + '.dbo.RegisterSession RSES
			ON RSES.RegisterID = REG.RegisterID
			AND RSES.Date = REGSC.Date
			AND FORMAT ( RSES.StartTime, ''HH:mm:ss'' ) = FORMAT ( REGSC.StartTime, ''HH:mm:ss'' )
			AND FORMAT ( RSES.EndTime, ''HH:mm:ss'' ) = FORMAT ( REGSC.EndTime, ''HH:mm:ss'' )
		WHERE
			REG.AcademicYearID = @AcademicYear
	
		UNION

		SELECT
			RegisterID = RSES.RegisterID,
			RegisterScheduleID = REGSC.RegisterScheduleID,
			RegisterSessionID = RSES.RegisterSessionID,
			Date = RSES.Date,
			StartTime = FORMAT ( RSES.StartTime, ''HH:mm:ss'' ),
			EndTime = FORMAT ( RSES.EndTime, ''HH:mm:ss'' ),
			Duration = RSES.Duration
		FROM ' + @System + '.dbo.Register REG
		INNER JOIN ' + @System + '.dbo.RegisterSession RSES
			ON RSES.RegisterID = REG.RegisterID
		LEFT JOIN ' + @System + '.dbo.RegisterSchedule REGSC
			ON REGSC.RegisterID = REG.RegisterID
			AND REGSC.Date = RSES.Date
			AND FORMAT ( REGSC.StartTime, ''HH:mm:ss'' ) = FORMAT ( RSES.StartTime, ''HH:mm:ss'' )
			AND FORMAT ( REGSC.EndTime, ''HH:mm:ss'' ) = FORMAT ( RSES.EndTime, ''HH:mm:ss'' )
		WHERE
			REG.AcademicYearID = @AcademicYear
	) RSES
		ON RSES.RegisterID = REG.RegisterID
	LEFT JOIN ' + @System + '.dbo.RegisterMark AS RM 
		ON RM.RegisterSessionID = RSES.RegisterSessionID
		AND RM.RegisterStudentID = REGS.RegisterStudentID
	LEFT JOIN ' + @System + '.dbo.MarkType AS MT 
		ON MT.MarkTypeID = RM.MarkTypeID
	LEFT JOIN ' + @System + '.dbo.MarkTypeStatus MTS
		ON MTS.MarkTypeStatusID = MT.MarkTypeStatusID
	LEFT JOIN ' + @System + '.dbo.OfferingGroup GRP
        ON GRP.OfferingGroupID = ENR.OfferingGroupID
    WHERE
        SD.AcademicYearID = @AcademicYear
        AND CAST ( RSES.Date AS DATE ) <= CAST ( GETDATE() AS DATE )
    GROUP BY
        SD.StudentDetailID,
        ENR.OfferingID,
        ENR.OfferingGroupID
	'

	SET @SQLString += 
        N'
	--Attendance by Learner
	DROP TABLE IF EXISTS #Attend
	SELECT
		ATT.StudentDetailID,
		Courses = COUNT ( DISTINCT ATT.OfferingID ),
		Planned = SUM ( ATT.Planned ),
        Counted = SUM ( ATT.Counted ),
        Marked = SUM ( ATT.Marked ),
        Unmarked = SUM ( ATT.Unmarked ),
        Present = SUM ( ATT.Present ),
        Absent = SUM ( ATT.Absent ),
        Neutral = SUM ( ATT.Neutral ),
        AuthAbsence = SUM ( ATT.AuthAbsence ),
        Late = SUM ( ATT.Late ),
        AttendPer = 
            ROUND (
                CASE
                    WHEN SUM ( ATT.Counted ) = 0 THEN 0
                    ELSE 
                        CAST ( SUM ( ATT.Present ) AS FLOAT )
                        /
                        CAST ( SUM ( ATT.Counted ) AS FLOAT )
                END
            , 4 )
		INTO #Attend
	FROM #AttendC ATT
	GROUP BY
		ATT.StudentDetailID
	'

	SET @SQLString += 
        N'
	--Risk
	DROP TABLE IF EXISTS #Risk
	SELECT
        StudentRef = S.StudentID,
        CourseCode = CRS.CourseCode,
        RiskCode = RSK.EnrolmentAtRiskStatus,
        RiskName = RSKS.AtRiskStatus,
        RiskColour = RSKS.Colour
		INTO #Risk
    FROM PROMONITOR.ProMonitor.dbo.Student S
    INNER JOIN PROMONITOR.ProMonitor.dbo.LearnerInformation_AtRisk RSK
        ON RSK.PMStudentID = S.PMStudentID
    INNER JOIN PROMONITOR.ProMonitor.dbo.CourseAtRiskStatus RSKS
        ON RSKS.CourseAtRiskID = RSK.EnrolmentAtRiskStatus
    INNER JOIN PROMONITOR.ProMonitor.dbo.Enrolment ENR
        ON ENR.PMEnrolmentID = RSK.PMEnrolmentID
    INNER JOIN PROMONITOR.ProMonitor.dbo.Course CRS
        ON CRS.CourseID = ENR.CourseID
    WHERE
        S.AcademicYearID = @AcademicYear
	'

	SET @SQLString += 
        N'
	--Not Yet Applied
	DROP TABLE IF EXISTS #NotYetApplied
	SELECT 
		SD.AcademicYearID,
		SD.StudentID,
		SD.StudentDetailID,
		NumApps = COUNT ( CRS.OfferingID ),
		Courses = 
			STUFF (
				(
					SELECT
						'', '' + CRS2.Code + '' - '' + REPLACE ( CRS2.Name, '','', ''&#44;'' )
					FROM ' + @System + '.dbo.Offering CRS2
					INNER JOIN ' + @System + '.dbo.ApplicationCourse APPC2
						ON APPC2.OfferingID = CRS2.OfferingID
					INNER JOIN ' + @System + '.dbo.Application APP2
						ON APP2.ApplicationID = APPC2.ApplicationID
					WHERE
						CRS2.AcademicYearID = SD.AcademicYearID
						AND APP2.StudentDetailID = SD.StudentDetailID
						AND COALESCE ( APP2.CollegeDecisionID, 1 ) <> 0
						AND COALESCE ( APP2.DecisionID, 1 ) = 1
					ORDER BY
						'', '' + CRS2.Code + '' - '' + REPLACE ( CRS2.Name, '','', ''&#44;'' ) 
						FOR XML PATH(''''),
						TYPE
				).value(''.'',''varchar(max)''), 1, 2, ''''
			)
			INTO #NotYetApplied
	FROM ' + @System + '.dbo.StudentDetail SD
	INNER JOIN ' + @System + '.dbo.Application APP
		ON APP.StudentDetailID = SD.StudentDetailID
	INNER JOIN ' + @System + '.dbo.ApplicationCourse APPC
		ON APPC.ApplicationID = APP.ApplicationID
	INNER JOIN ' + @System + '.dbo.Offering CRS
		ON CRS.OfferingID = APPC.OfferingID
	WHERE
		SD.AcademicYearID = @NextYear
		AND COALESCE ( APP.CollegeDecisionID, 1 ) <> 0
		AND COALESCE ( APP.DecisionID, 1 ) = 1
	GROUP BY
		SD.AcademicYearID,
		SD.StudentID,
		SD.StudentDetailID
	'

	SET @SQLString += 
        N'
	--Not Yet Enrolled
	DROP TABLE IF EXISTS #NotYetEnrolled
	SELECT 
        SD.AcademicYearID,
        SD.StudentID,
		SD.StudentDetailID,
        NumEnrols = COUNT ( CRS.OfferingID ),
        Courses = 
            STUFF (
                (
                        SELECT
                            '', '' + CRS2.Code + '' - '' + REPLACE ( CRS2.Name, '','', ''&#44;'' )
                        FROM ' + @System + '.dbo.Offering CRS2
                        INNER JOIN ' + @System + '.dbo.Enrolment ENR2
                            ON ENR2.OfferingID = CRS2.OfferingID
                        WHERE
                            CRS2.AcademicYearID = SD.AcademicYearID
							AND ENR2.StudentDetailID = SD.StudentDetailID
							AND ENR2.CompletionStatusID IN ( ''1'', ''2'', ''3'' )
                        ORDER BY
                            '', '' + CRS2.Code + '' - '' + REPLACE ( CRS2.Name, '','', ''&#44;'' ) 
							FOR XML PATH(''''),
                            TYPE
                ).value(''.'',''varchar(max)''), 1, 2, ''''
            )
			INTO #NotYetEnrolled
    FROM ' + @System + '.dbo.StudentDetail SD
    INNER JOIN ' + @System + '.dbo.Enrolment ENR
        ON ENR.StudentDetailID = SD.StudentDetailID
    INNER JOIN ' + @System + '.dbo.Offering CRS
        ON CRS.OfferingID = ENR.OfferingID
    WHERE
        SD.AcademicYearID = @NextYear
        AND ENR.CompletionStatusID IN ( ''1'', ''2'', ''3'' )
    GROUP BY
        SD.AcademicYearID,
        SD.StudentID,
		SD.StudentDetailID
	'

	SET @SQLString += 
        N'
	--Current Destinations
	DROP TABLE IF EXISTS #Destinations
	SELECT
        OC.StudentID,
        OC.LocalDestinationID,
		OC.IsActualDestination,
        RowNum = 
            ROW_NUMBER () OVER (
                PARTITION BY
                    OC.StudentID
                ORDER BY
                    OC.IsActualDestination,
                    CASE WHEN OC.EndDate IS NULL THEN 1 ELSE 2 END,
                    OC.CollectionDate DESC,
                    OC.StartDate DESC
            )
			INTO #Destinations
    FROM ' + @System + '.dbo.StudentOutcome OC
    WHERE
        OC.ReturnYearID = @DestinationYear

	'

	SET @SQLString += 
        N'
	--Main Query
	SELECT
		SystemDatabase = @System,
        AcademicYear = SD.AcademicYearID,
		CollegeCode = COALESCE ( RTRIM ( CGRP.Code ), ''--UNK--'' ),
		CollegeName = COALESCE ( CGRP.Name, ''--Unknown--'' ),
		SiteCode = STE.Code,
		SiteName = STE.Description,
		FacCode = COALESCE ( RTRIM ( FAC.Code ), ''--UNK--'' ),
		FacName = COALESCE ( FAC.Name, ''--Unknown--'' ),
		TeamCode = RTRIM ( TEAM.Code ),
		TeamName = TEAM.Name,
		CourseCode = CRS.Code,
		CourseTitle = CRS.Name,
        StudentRef = RTRIM ( SD.RefNo ),
		Surname = SD.Surname,
		Forename = SD.FirstForename,
		DOB = SD.DateOfBirth,
		Age31stAug = CAST ( FLOOR ( DATEDIFF ( DAY, COALESCE ( SD.DateOfBirth, CAST ( ''20'' + LEFT ( SD.AcademicYearID, 2 ) + ''-09-01'' AS DATETIME2 ) ), CAST ( ''20'' + LEFT ( SD.AcademicYearID, 2 ) + ''-09-01'' AS DATETIME2 ) ) / 365.23076923074 ) AS int ),
		CompletionStatusCode = ENR.CompletionStatusID,
		CompletionStatusName = CMP.Description,
		ClassesCourses = COALESCE ( ATT.Courses, 0 ),
        ClassesPlanned = COALESCE ( ATT.Planned, 0 ),
        ClassesCounted = COALESCE ( ATT.Counted, 0 ),
        ClassesMarked = COALESCE ( ATT.Marked, 0 ),
        ClassesUnmarked = COALESCE ( ATT.Unmarked, 0 ),
        ClassesPresent = COALESCE ( ATT.Present, 0 ),
        ClassesAbsent = COALESCE ( ATT.Absent, 0 ),
        ClassesNeutral = COALESCE ( ATT.Neutral, 0 ),
        ClassesAuthAbsence = COALESCE ( ATT.AuthAbsence, 0 ),
        ClassesLate = COALESCE ( ATT.Late, 0 ),
        AttendPer = CAST ( COALESCE ( ATT.AttendPer, 0 ) AS FLOAT ),
        RiskCode = COALESCE ( RSK.RiskCode, 0 ),
        RiskName = COALESCE ( RSK.RiskName, ''Risk Not Set''),
        RiskColour = COALESCE ( RSK.RiskColour, ''Red'' ),
		OnTrackToAchieveCode = COALESCE ( ENR.UserDefined10, ENR.UserDefined9, ENR.UserDefined8, ''Not Set'' ),
		OnTrackToAchieveColour = 
			CASE
				WHEN COALESCE ( ENR.UserDefined10, ENR.UserDefined9, ENR.UserDefined8 ) = ''R'' THEN ''Red''
				WHEN COALESCE ( ENR.UserDefined10, ENR.UserDefined9, ENR.UserDefined8 ) = ''A'' THEN ''Orange''
				WHEN COALESCE ( ENR.UserDefined10, ENR.UserDefined9, ENR.UserDefined8 ) = ''G'' THEN ''Green''
				ELSE ''Red''
			END,
		OnTrackToAchieveTerm = 
			CASE
				WHEN ENR.UserDefined10 IS NOT NULL THEN ''Term 3''
				WHEN ENR.UserDefined9 IS NOT NULL THEN ''Term 2''
				WHEN ENR.UserDefined8 IS NOT NULL THEN ''Term 1''
				ELSE ''None''
			END,
		NumAppsNextYear = COALESCE ( NYA.NumApps, 0 ),
        AppliedCoursesNextYear = NYA.Courses,
        NumEnrolsNextYear = COALESCE ( NYE.NumEnrols, 0 ),
        EnrolledCoursesNextYear = NYE.Courses,
		NoProgressionRoute = 
			CASE
				WHEN NPR.StudentRef IS NOT NULL THEN
					CASE
						WHEN COALESCE ( NYA.NumApps, 0 ) = 0
						AND COALESCE ( NYE.NumEnrols, 0 ) = 0
							THEN ''Y''
						ELSE ''N''
					END
				ELSE ''N''
			END,
		NoProgressionRouteReason = NPR.Notes,
		NoProgressionRouteCreatedDate = NPR.CreatedDate,
		NoProgressionRouteCreatedBy = NPR.CreatedBy,
		NoProgressionRouteUpdatedDate = NPR.UpdatedDate,
		NoProgressionRouteUpdatedBy = NPR.UpdatedBy,
        DestinationCode = DES.LocalDestinationID,
        DestinationName = DES.Description,
		DestinationIsActual = CAST ( OC.IsActualDestination AS INT ),
        DestinationChanged = CAST ( 0 AS bit ),
		ProgressLearner = CAST ( 0 AS bit ),
        OfferType = 0,
        OfferCondition = 0,
		IsReadyToEnrol = CAST ( 0 AS bit ),
		ReadyToEnrolOption = '''',
		HasAlreadyApplied = CAST ( 0 AS bit )
	'

    SET @SQLString += 
        N'
	FROM ' + @System + '.dbo.StudentDetail SD
	INNER JOIN ' + @System + '.dbo.Enrolment ENR
		ON ENR.StudentDetailID = SD.StudentDetailID
    INNER JOIN ' + @System + '.dbo.Offering CRS
        ON CRS.OfferingID = ENR.OfferingID
	INNER JOIN ' + @System + '.dbo.CompletionStatus CMP
		ON CMP.CompletionStatusID = ENR.CompletionStatusID
	INNER JOIN ' + @System + '.dbo.Site STE
		ON STE.SiteID = CRS.SiteID
	INNER JOIN ' + @System + '.dbo.CollegeLevel TEAM
		ON TEAM.SID = CRS.SID
	LEFT JOIN ' + @System + '.dbo.CollegeLevel FAC
		ON FAC.SID = TEAM.ParentSID
	LEFT JOIN ' + @System + '.dbo.CollegeLevel CGRP
		ON CGRP.SID = FAC.ParentSID
    LEFT JOIN #Destinations OC
        ON OC.StudentID = SD.StudentID
        AND OC.RowNum = 1
    LEFT JOIN ' + @System + '.dbo.LocalDestination DES
        ON DES.LocalDestinationID = COALESCE ( OC.LocalDestinationID, SD.LocalDestinationID )
    
    LEFT JOIN #Attend ATT
        ON ATT.StudentDetailID = SD.StudentDetailID
    
    LEFT JOIN #AttendC ATTC
        ON ATTC.StudentDetailID = SD.StudentDetailID
        AND ATTC.OfferingID = ENR.OfferingID
        AND COALESCE ( ATTC.OfferingGroupID, 0 ) = COALESCE ( ENR.OfferingGroupID, 0 )
    
    LEFT JOIN #Risk RSK
        ON RSK.StudentRef = SD.RefNo
        AND RSK.CourseCode = CRS.Code
	LEFT JOIN #NotYetApplied NYA
        ON NYA.StudentID = SD.StudentID
	
    LEFT JOIN #NotYetEnrolled NYE
        ON NYE.StudentID = SD.StudentID
	LEFT JOIN WLC.dbo.PRG_NoProgressionRoute NPR
		ON NPR.StudentRef = SD.RefNo
		AND NPR.AcademicYear = @NextYear
		AND NPR.OfferingID = CRS.OfferingID
		AND NPR.SystemDatabase = ''' + @System + '''
	WHERE
		SD.AcademicYearID = @AcademicYear
		AND ENR.CompletionStatusID IN ( ''1'', ''2'' )
		AND COALESCE ( ENR.ActualEndDate, ENR.ExpectedEndDate ) BETWEEN ''20'' + LEFT ( @AcademicYear, 2 ) + ''-08-01'' AND ''20'' + RIGHT ( @AcademicYear, 2 ) + ''-07-31''
		AND ENR.OfferingID = @CourseID
		AND COALESCE ( ENR.OfferingGroupID, 0 ) = COALESCE ( @GroupID, 0 )
	ORDER BY
		SD.Surname,
		SD.FirstForename,
		SD.RefNo
	'

	--SELECT @SQLString AS [processing-instruction(x)] FOR XML PATH('')

    SET @SQLParams = 
        N'@System NVARCHAR(50),
        @SystemILP NVARCHAR(50),
        @AcademicYear NVARCHAR(5),
		@NextYear NVARCHAR(5),
        @DestinationYear NVARCHAR(5),
        @CourseID int,
	    @GroupID int';
    
    EXECUTE sp_executesql 
        @SQLString, 
        @SQLParams, 
        @System = @System, 
        @SystemILP = @SystemILP, 
        @AcademicYear = @AcademicYear,
		@NextYear = @NextYear, 
        @DestinationYear = @DestinationYear, 
        @CourseID = @CourseID,
        @GroupID = @GroupID;
END